---
layout: post
title: AS3.0 で TextField をマスクに使う簡単な方法
tags: ActionScript
lang: ja
old_url: http://d.hatena.ne.jp/nitoyon/20090909/text_mask
---
><center><img src="http://img.f.hatena.ne.jp/images/fotolife/n/nitoyon/20090909/20090909004458.png" width="442" height="63"></center><

以前、{% post_link 2007-09-11-mask-letter %} という記事を書いたけど、<code>BitmapData</code> を使わなくても文字列でマスクを作れることに気がついた。

キモは <code>cacheAsBitmap</code> プロパティ。こいつを利用すれば内部的に <code>BitmapData</code> を作成してくれるので、<code>TextField</code> をマスクとしても利用できちゃう。

完成品はこんなの。

><center><script src="http://www.gmodules.com/ig/ifr?url=http://nitoyon.googlepages.com/embed_flash.xml&amp;up_url=http%3A%2F%2Ftech.nitoyon.com%2Fmisc%2Fswf%2FEasyDynamicTextMask.swf&amp;up_background=%23ffffff&amp;synd=open&amp;w=450&amp;h=100&amp;title=Easy+Dynamic+Text+Mask&amp;border=%23ffffff%7C3px%2C1px+solid+%23999999&amp;output=js"></script><noscript>Flash Player 9 以上がインストールされていると、ここに完成品が表示されますよ。</noscript></center><

文字を編集することもできる。編集後の文字でもマスクされる！

**やってること
+<code>TextField</code> を表示
+マスクされる <code>Sprite</code> を作成
+<code>Sprite</code> のマスクを <code>TextField</code> に設定する
+<code>TextField</code> と <code>Sprite</code> の両方の <code>cacheAsBitmap</code> を <code>true</code> にする

ソースコードは以下に（39行）。

>|javascript|
// Easy Dynamic Text Mask
package{
import flash.display.*;
import flash.events.Event;
import flash.text.*;

[SWF(backgroundColor="#ffffff", width="450", height="80")]
public class EasyDynamicTextMask extends Sprite{
    public function EasyDynamicTextMask(){
        stage.scaleMode = "noScale";

        // Show original text (input)
        var text:TextField = new TextField();
        text.type = "input";
        text.autoSize = "left";
        text.htmlText = <font size="50" color="#000000">HELLO WORLD</font>.toXMLString();
        addChild(text);

        // Create sprite
        var sprite:Sprite = new Sprite();
        addChild(sprite);

        // Set text as mask
        sprite.mask = text;
        text.cacheAsBitmap = sprite.cacheAsBitmap = true;
        
        // Draw sprite
        addEventListener("enterFrame", function(event:Event):void{
            sprite.graphics.beginFill(Math.random() * 0xffffff);
            sprite.graphics.drawCircle(Math.random() * 450, Math.random() * 80, Math.random() * 50);
            sprite.graphics.endFill();

            if (Math.random() < .02){
                sprite.graphics.clear();
            }
        });
    }
}
}
||<

><div class="seealso"><
**あわせてどうぞ
-{% post_link 2009-09-08-easy-open-face %}
-{% post_link 2009-09-07-glowing-text %}
-{% post_link 2008-07-25-shape-of-a-dream %}
-{% post_link 2008-07-24-embed-font-swfassist %}
-{% post_link 2007-10-05-potras-pop %}
></div><

